
Процедура ОбработкаПроведения(Отказ, Режим)
	
	ОбработкаПроведенияОУ(Отказ);
	ОбработкаПроведенияБУ(Отказ);

		
	//{{__КОНСТРУКТОР_ДВИЖЕНИЙ_РЕГИСТРОВ
	// Данный фрагмент построен конструктором.
	// При повторном использовании конструктора, внесенные вручную изменения будут утеряны!!!


	//}}__КОНСТРУКТОР_ДВИЖЕНИЙ_РЕГИСТРОВ
КонецПроцедуры

Процедура ОбработкаПроведенияОУ(Отказ)
	УчетнаяПолитика = РегистрыСведений.УчетнаяПолитика.СрезПоследних(МоментВремени());
	
	Если УчетнаяПолитика.Количество() = 0 Тогда
		Отказ = Истина;
	Иначе
		МетодСписания = УчетнаяПолитика[0].МетодСписания;
		Если НЕ ЗначениеЗаполнено(МетодСписания) Тогда
			Отказ = Истина;
		КонецЕсли;
	КонецЕсли;	
	
	Если Отказ Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Не задана Учетная политика!";
		Сообщение.Сообщить();
		Возврат;
	КонецЕсли;
	
	Движения.ПартииНоменклатуры.Записывать = Истина;
	Движения.ПартииНоменклатуры.Записать();
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.ПартииНоменклатуры");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.ИсточникДанных = СписокНоменклатуры;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Номенклатура", "Номенклатура");
	Блокировка.Заблокировать();
		
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	&Склад КАК Склад,
		|	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура КАК Номенклатура,
		|	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Количество) КАК Количество,
		|	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Сумма) КАК Сумма
		|ПОМЕСТИТЬ ВТ_Товары
		|ИЗ
		|	Документ.РасходнаяНакладная.СписокНоменклатуры КАК РасходнаяНакладнаяСписокНоменклатуры
		|ГДЕ
		|	РасходнаяНакладнаяСписокНоменклатуры.Ссылка = &Ссылка
		|	И РасходнаяНакладнаяСписокНоменклатуры.Номенклатура.ВидНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ВидыНоменклатуры.Товар)
		|
		|СГРУППИРОВАТЬ ПО
		|	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_Товары.Склад КАК Склад,
		|	ВТ_Товары.Номенклатура КАК Номенклатура,
		|	ВТ_Товары.Количество КАК Количество,
		|	ВТ_Товары.Сумма КАК Сумма,
		|	ПартииНоменклатурыОстатки.Партия КАК Партия,
		|	ЕСТЬNULL(ПартииНоменклатурыОстатки.КоличествоПартииОстаток, 0) КАК КоличествоПартии,
		|	ЕСТЬNULL(ПартииНоменклатурыОстатки.СуммаПартииОстаток, 0) КАК СуммаПартии,
		|	ВТ_Товары.Номенклатура.Представление КАК НоменклатураПредставление
		|ИЗ
		|	ВТ_Товары КАК ВТ_Товары
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ПартииНоменклатуры.Остатки(
		|				&МоментВремени,
		|				Номенклатура В
		|					(ВЫБРАТЬ
		|						ВТ_Товары.Номенклатура КАК Номенклатура
		|					ИЗ
		|						ВТ_Товары КАК ВТ_Товары)) КАК ПартииНоменклатурыОстатки
		|		ПО ВТ_Товары.Номенклатура = ПартииНоменклатурыОстатки.Номенклатура
		|
		|УПОРЯДОЧИТЬ ПО
		|	ПартииНоменклатурыОстатки.Партия.МоментВремени УБЫВ
		|ИТОГИ
		|	МАКСИМУМ(Количество),
		|	СУММА(КоличествоПартии)
		|ПО
		|	Номенклатура";
	
	Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
	Запрос.УстановитьПараметр("Склад", Склад);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	Если МетодСписания = Перечисления.УчетнаяПолитика.ФИФО Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, ".МоментВремени УБЫВ", ".МоментВремени");
	КонецЕсли;	
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Отказ = Истина;
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Нет партий по указанным номенклатурам!";
		Сообщение.Сообщить();
		Возврат;
	КонецЕсли;	
	
	ВыборкаНоменклатура = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаНоменклатура.Следующий() Цикл
		Движение = Движения.ОстаткиНоменклатуры.ДобавитьРасход();
		Движение.Период = Дата;
		Движение.Номенклатура = ВыборкаНоменклатура.Номенклатура;
		Движение.Склад = Склад;
		Движение.Количество = ВыборкаНоменклатура.Количество;
		
		НеХватаетКолво = ВыборкаНоменклатура.КоличествоПартии - ВыборкаНоменклатура.Количество;
		
		Если НеХватаетКолво < 0 Тогда
			Отказ = Истина;
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = "Не хватает партий по товару: "+ВыборкаНоменклатура.НоменклатураПредставление+" в кол-ве: "+НеХватаетКолво;
			Сообщение.Сообщить();
		КонецЕсли;
		
		Если Отказ Тогда
			Продолжить;	
		КонецЕсли;
		
		КоличествоСписать = ВыборкаНоменклатура.Количество;
		
		Выборка = ВыборкаНоменклатура.Выбрать();
	
		Пока КоличествоСписать > 0 И Выборка.Следующий() Цикл
			Себестоимость = КоличествоСписать / Выборка.КоличествоПартии * Выборка.СуммаПартии;
			
			Движение = Движения.ПартииНоменклатуры.ДобавитьРасход();
			Движение.Период = Дата;
			Движение.Номенклатура = Выборка.Номенклатура;
			Движение.Партия = Выборка.Партия;
			Движение.КоличествоПартии = КоличествоСписать;
			Движение.СуммаПартии = Себестоимость;
			
			КоличествоСписать = КоличествоСписать - Выборка.КоличествоПартии;
		КонецЦикла;
		
	КонецЦикла;
	
	Движения.ОстаткиНоменклатуры.БлокироватьДляИзменения = Истина;
	Движения.ОстаткиНоменклатуры.Записать();
		
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ОстаткиНоменклатурыОстатки.Номенклатура КАК Номенклатура,
		|	ОстаткиНоменклатурыОстатки.Склад КАК Склад,
		|	ОстаткиНоменклатурыОстатки.КоличествоОстаток КАК КоличествоОстаток,
		|	ОстаткиНоменклатурыОстатки.Номенклатура.Представление КАК НоменклатураПредставление,
		|	ОстаткиНоменклатурыОстатки.Склад.Представление КАК СкладПредставление
		|ИЗ
		|	РегистрНакопления.ОстаткиНоменклатуры.Остатки(
		|			&Граница,
		|			(Номенклатура, Склад) В
		|				(ВЫБРАТЬ РАЗЛИЧНЫЕ
		|					ВТ.Номенклатура,
		|					ВТ.Склад
		|				ИЗ
		|					ВТ_Товары КАК ВТ)) КАК ОстаткиНоменклатурыОстатки
		|ГДЕ
		|	ОстаткиНоменклатурыОстатки.КоличествоОстаток < 0";
	
	Запрос.УстановитьПараметр("Граница", Новый Граница(МоментВремени(), ВидГраницы.Включая));
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		// Минусов нет
	Иначе
		Отказ = Истина;
	
		Выборка = РезультатЗапроса.Выбрать();
		
		Пока Выборка.Следующий() Цикл
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = "Не хватает остатка по "+Выборка.НоменклатураПредставление+" на складе: "+Выборка.СкладПредставление+" в кол-ве: "+Выборка.КоличествоОстаток;
			Сообщение.Сообщить();	
		КонецЦикла;
	КонецЕсли;	
КонецПроцедуры	

Процедура ОбработкаПроведенияБУ(Отказ)

// регистр Управленческий 
	Движения.Управленческий.Записывать = Истина;
	Движения.Управленческий.Записать();
	
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрБухгалтерии.Управленческий");
	ЭлементБлокировки.УстановитьЗначение("Счет", ПланыСчетов.Управленческий.Товары);
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.ИсточникДанных = СписокНоменклатуры;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных(ПланыВидовХарактеристик.ВидыСубконто.Номенклатура, "Номенклатура");
	Блокировка.Заблокировать();
	
	 	//{{КОНСТРУКТОР_ЗАПРОСА_С_ОБРАБОТКОЙ_РЕЗУЛЬТАТА
	// Данный фрагмент построен конструктором.
	// При повторном использовании конструктора, внесенные вручную изменения будут утеряны!!!
	
		
	//}}КОНСТРУКТОР_ЗАПРОСА_С_ОБРАБОТКОЙ_РЕЗУЛЬТАТА

	
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура КАК Номенклатура,
		|	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Количество) КАК КоличествоДок,
		|	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Сумма) КАК СуммаДок
		|ПОМЕСТИТЬ ВТ_Товары
		|ИЗ
		|	Документ.РасходнаяНакладная.СписокНоменклатуры КАК РасходнаяНакладнаяСписокНоменклатуры
		|ГДЕ
		|	РасходнаяНакладнаяСписокНоменклатуры.Ссылка = &Ссылка
		|	И РасходнаяНакладнаяСписокНоменклатуры.Номенклатура.ВидНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ВидыНоменклатуры.Товар)
		|
		|СГРУППИРОВАТЬ ПО
		|	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_Товары.Номенклатура КАК Номенклатура,
		|	ВТ_Товары.КоличествоДок КАК КоличествоДок,
		|	ВТ_Товары.СуммаДок КАК СуммаДок,
		|	ЕСТЬNULL(УправленческийОстатки.КоличествоОстаток, 0) КАК КоличествоОстаток,
		|	ЕСТЬNULL(УправленческийОстатки.СуммаРубОстаток, 0) КАК СуммаОстаток,
		|	ВТ_Товары.Номенклатура.Представление КАК НоменклатураПредставление,
		|	УправленческийОстатки.Субконто2 КАК СрокГодности
		|ИЗ
		|	ВТ_Товары КАК ВТ_Товары
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Управленческий.Остатки(
		|				&МоментВремени,
		|				Счет = &СчетТовары,
		|				&Субконто,
		|				Субконто1 В
		|					(ВЫБРАТЬ
		|						ВТ_Товары.Номенклатура КАК Номенклатура
		|					ИЗ
		|						ВТ_Товары КАК ВТ_Товары)) КАК УправленческийОстатки
		|		ПО ВТ_Товары.Номенклатура = УправленческийОстатки.Субконто1
		|
		|УПОРЯДОЧИТЬ ПО
		|	СрокГодности
		|ИТОГИ
		|	МАКСИМУМ(КоличествоДок),
		|	СУММА(КоличествоОстаток),
		|	СУММА(СуммаОстаток),
		|	МАКСИМУМ(СуммаДок)
		|ПО
		|	Номенклатура";
	
	Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Субконто = новый Массив;
	Субконто.Добавить(ПланыВидовХарактеристик.ВидыСубконто.Номенклатура);
	Субконто.Добавить(ПланыВидовХарактеристик.ВидыСубконто.СрокГодности);
	Запрос.УстановитьПараметр("Субконто", Субконто);
	Запрос.УстановитьПараметр("СчетТовары", ПланыСчетов.Управленческий.Товары);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаНоменклатура = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Сумма = 0;
	
	Пока ВыборкаНоменклатура.Следующий() Цикл
		НеХватает = ВыборкаНоменклатура.КоличествоДок - ВыборкаНоменклатура.КоличествоОстаток;
        Если НеХватает > 0 Тогда
            Отказ = Истина;
            Сообщение = Новый СообщениеПользователю;
            Сообщение.Текст = "Не хватает товара (бух. учет): " + ВыборкаНоменклатура.НоменклатураПредставление + " в количестве: " + НеХватает;
            Сообщение.Сообщить();
		КонецЕсли;
		
		Если Отказ Тогда
		
			Продолжить;
		
		КонецЕсли;
		
		КоличествоСписать = ВыборкаНоменклатура.КоличествоДок;
		КоличествоПартийИтого = ВыборкаНоменклатура.КоличествоОстаток;
		СуммаСписано = 0;
		
		Выборка = ВыборкаНоменклатура.Выбрать();
		
		Пока Выборка.Следующий() И КоличествоСписать > 0 Цикл
			МинимальноеКоличество = Мин(Выборка.КоличествоОстаток, КоличествоСписать);
			СуммаСписания = ?(КоличествоПартийИтого = МинимальноеКоличество, 
                ВыборкаНоменклатура.СуммаОстаток - СуммаСписано,
                МинимальноеКоличество / ВыборкаНоменклатура.КоличествоОстаток * ВыборкаНоменклатура.СуммаОстаток);
			
			Движение = Движения.Управленческий.Добавить();
			Движение.СчетДт 		= ПланыСчетов.Управленческий.ПрибылиУбытки;
			Движение.СчетКт 		= ПланыСчетов.Управленческий.Товары;
			Движение.Период 		= Дата;
			Движение.КоличествоКт 	= МинимальноеКоличество;	
			Движение.СуммаРуб 		= СуммаСписания;
			Движение.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.Номенклатура] = Выборка.Номенклатура;
			Движение.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.СрокГодности] = Выборка.СрокГодности;
						
			КоличествоСписать = КоличествоСписать - МинимальноеКоличество;
			КоличествоПартийИтого = КоличествоПартийИтого - Движение.КоличествоКт;
			СуммаСписано = СуммаСписано + Движение.СуммаРуб;
		КонецЦикла;	
		
		Сумма = Сумма + ВыборкаНоменклатура.СуммаДок;
		
	КонецЦикла;
	
	Движение = Движения.Управленческий.Добавить();
	Движение.СчетДт 		= ПланыСчетов.Управленческий.Покупатели;
	Движение.СчетКт 		= ПланыСчетов.Управленческий.ПрибылиУбытки;
	Движение.Период 		= Дата;
	Движение.СуммаРуб 		= Сумма;

КонецПроцедуры
