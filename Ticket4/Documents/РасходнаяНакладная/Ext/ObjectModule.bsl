
Процедура ОбработкаПроведения(Отказ, Режим)	
	
	// ДВИЖЕНИЯ ПО РЕГИСТРУ ОСТАТКИ НОМЕНКЛАТУРЫ (СТАРАЯ МЕТОДИКА ПРОВЕДЕНИЯ)
	
	Движения.ОстаткиНоменклатуры.Записывать = Истина;
	Движения.ОстаткиНоменклатуры.Записать();

	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.ОстаткиНоменклатуры");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.ИсточникДанных = СписокНоменклатуры;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Номенклатура", "Номенклатура");
	Блокировка.Заблокировать();
		
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура КАК Номенклатура,
		|	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Количество) КАК Количество
		|ПОМЕСТИТЬ ВТ_Товары
		|ИЗ
		|	Документ.РасходнаяНакладная.СписокНоменклатуры КАК РасходнаяНакладнаяСписокНоменклатуры
		|ГДЕ
		|	РасходнаяНакладнаяСписокНоменклатуры.Ссылка = &Ссылка
		|	И РасходнаяНакладнаяСписокНоменклатуры.Номенклатура.ВидНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ВидыНоменклатуры.Товар)
		|
		|СГРУППИРОВАТЬ ПО
		|	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_Товары.Номенклатура КАК Номенклатура,
		|	ВТ_Товары.Количество КАК Количество,
		|	ОстаткиНоменклатурыОстатки.Склад КАК Склад,
		|	ЕСТЬNULL(ОстаткиНоменклатурыОстатки.КоличествоОстаток, 0) КАК КоличествоОстаток,
		|	ЕСТЬNULL(ОстаткиНоменклатурыОстатки.СуммаОстаток, 0) КАК СуммаОстаток
		|ПОМЕСТИТЬ ВТ_ТоварыОстатки
		|ИЗ
		|	ВТ_Товары КАК ВТ_Товары
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ОстаткиНоменклатуры.Остатки(
		|				&МоментВремени,
		|				Номенклатура В
		|					(ВЫБРАТЬ
		|						ВТ_Товары.Номенклатура КАК Номенклатура
		|					ИЗ
		|						ВТ_Товары КАК ВТ_Товары)) КАК ОстаткиНоменклатурыОстатки
		|		ПО ВТ_Товары.Номенклатура = ОстаткиНоменклатурыОстатки.Номенклатура
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Склад
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_ТоварыОстатки.Номенклатура КАК Номенклатура,
		|	ВТ_ТоварыОстатки.Количество КАК Количество,
		|	ВТ_ТоварыОстатки.Склад КАК Склад,
		|	ВТ_ТоварыОстатки.КоличествоОстаток КАК КоличествоОстаток,
		|	ВТ_ТоварыОстатки.СуммаОстаток КАК СуммаОстаток,
		|	ВЫБОР
		|		КОГДА ВТ_ТоварыОстатки.Склад = ПриоритетСкладовСрезПоследних.Склад
		|			ТОГДА 0
		|		ИНАЧЕ ЕСТЬNULL(ПриоритетСкладовСрезПоследних.Приоритет, 99999)
		|	КОНЕЦ КАК Приоритет,
		|	ВТ_ТоварыОстатки.Номенклатура.Представление КАК НоменклатураПредставление
		|ИЗ
		|	ВТ_ТоварыОстатки КАК ВТ_ТоварыОстатки,
		|	РегистрСведений.ПриоритетСкладов.СрезПоследних(
		|			&МоментВремени,
		|			Склад В
		|				(ВЫБРАТЬ
		|					ВТ_ТоварыОстатки.Склад КАК Склад
		|				ИЗ
		|					ВТ_ТоварыОстатки КАК ВТ_ТоварыОстатки)) КАК ПриоритетСкладовСрезПоследних
		|
		|УПОРЯДОЧИТЬ ПО
		|	Приоритет
		|ИТОГИ
		|	МАКСИМУМ(Количество),
		|	СУММА(КоличествоОстаток)
		|ПО
		|	Номенклатура";
	
	Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаНоменклатура = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаНоменклатура.Следующий() Цикл
		// Вставить обработку выборки ВыборкаНоменклатура
	    Превышение = ВыборкаНоменклатура.Количество - ВыборкаНоменклатура.КоличествоОстаток;
		Если Превышение > 0 Тогда
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = СтрШаблон("Не хватает остатка по номенклатуре %1 в количестве %2",
								ВыборкаНоменклатура.НоменклатураПредставление,
								Превышение);
			Сообщение.Сообщить();
			Отказ = Истина;
		КонецЕсли;
		
		Если Отказ Тогда
			Продолжить;
		КонецЕсли;	
		
		КоличествоСписать = ВыборкаНоменклатура.Количество;
		
		ВыборкаДетальныеЗаписи = ВыборкаНоменклатура.Выбрать();
	
		Пока КоличествоСписать > 0 И ВыборкаДетальныеЗаписи.Следующий() Цикл
			КолПартии = Мин(КоличествоСписать, ВыборкаДетальныеЗаписи.КоличествоОстаток);	
			Себестоимость = КолПартии / ВыборкаДетальныеЗаписи.КоличествоОстаток * ВыборкаДетальныеЗаписи.СуммаОстаток;
			
			Движение = Движения.ОстаткиНоменклатуры.ДобавитьРасход();
			Движение.Период = Дата;
			Движение.Номенклатура = ВыборкаДетальныеЗаписи.Номенклатура;
			Движение.Склад = ВыборкаДетальныеЗаписи.Склад;
			Движение.Количество = КолПартии;
			Движение.Сумма = Себестоимость;
			
			КоличествоСписать = КоличествоСписать - КолПартии;		
		КонецЦикла;
	КонецЦикла;
	
	
	//{{__КОНСТРУКТОР_ДВИЖЕНИЙ_РЕГИСТРОВ
	// Данный фрагмент построен конструктором.
	// При повторном использовании конструктора, внесенные вручную изменения будут утеряны!!!

	// регистр ОстаткиНоменклатуры Расход
	
	//}}__КОНСТРУКТОР_ДВИЖЕНИЙ_РЕГИСТРОВ
КонецПроцедуры
